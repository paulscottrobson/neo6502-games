'
'		*** Asteroids ***
'
gload "asteroids.gfx":cls:sprite clear
call Initialise()

cls
call DrawLives()
call DrawScore()
call Reset.Structures()
call Reset.Player()
for i = 1 to 4
	call Create.Asteroid(-1,-1,0,rand(360))
next 

tmr.movedraw = 0:tmr.rotate = 0:tmr.last.fire = 0:tmr.checkbullet = 0
repeat
	if event(tmr.movedraw,4) then a = maxObjects:sys ASMoveDrawAll
	if event(tmr.rotate,2) then call Rotate.Player()
	if event(tmr.checkbullet,20) then call Check.Kill.Bullets()
	a = 0:sys ASCalculateNearest:print chr$(20);nearest[0],nearest[1],nearest[2];"   "
until false

end
'
'		Initialise data structures, arrays etc.
'
proc Initialise()
	local i
	maxObjects = 32:recordSize = 14:lives = 3:score = 0
	objectList = alloc((maxObjects+5)*2)
	'nearest = alloc(4)
	for i = 0 to maxObjects-1:objectList[i] = alloc(recordSize):next
	dim freeObjects(maxObjects),freeBullets(4),killBulletTime(4)
	call Machine.Code()
	doke oList,objectList
endproc
'
'		Reset the data structures. Disable all objects, create default free objects table for asteroids.
'
proc Reset.Structures()
	local i,r
	sprite clear
	freeObjects(0) = 0
	for i = 0 to maxObjects-1
		r = objectList[i]:r[0] = 0
		if i >= 5 then freeObjects(0) = freeObjects(0)+1:freeObjects(freeObjects(0)) = i
	next
	freeBullets(0) = 4
	for i = 1 to 4:freeBullets(i) = i:killBulletTime(i) = 0:next 
endproc
'
'		Create a new asteroid
'
proc Create.Asteroid(x,y,size,angle)
	local e,r
	if freeObjects(0) > 0 
		e = freeObjects(freeObjects(0))
		freeObjects(0) = freeObjects(0) - 1
		r = objectList[e]
		if size = 2:r[0] = $C0+rand(8):else:r[0]=(1-size)*8+$80+rand(8):endif
		if x < 0 then x = rand(160)
		if y < 0 then y = rand(120)
		r[1] = x * 256:r[3] = y * 256
		call Set.Object.Velocity(r,0.2+(2-size)/2,angle)
		r[6] = e
	endif
endproc
'
'		Set Speed/Angle of given object
'
proc Set.Object.Velocity(r,speed,angle)
	r[2] = int(sin(angle)*speed*256)
	r[4] = -int(cos(angle)*speed*256)
	r[5] = angle
endproc
'
'		Initialise Player
'
proc Reset.Player()
	local i,r
	for i = 1 to 4:r = objectList[i]:r[0] = 0:next
	r = objectList[0]
	call Set.Object.Velocity(r,0,0)
	r[0] = $90:r[1] = 80*256:r[3] = 60*256:r[6] = 0
endproc
'
'		Rotate Player
'
proc Rotate.Player()
	local fire,dx,dy
	fire = joypad(dx,dy)
	r = objectList[0]
	if dx
		r[5] = (r[5] + dx * 4 + 360) % 360
		r[0] = int(r[5] * 0.0888+0.5)+$90
	endif
	if dy < 0
		r[2] = r[2] + int(sin(r[5])*16)
		r[4] = r[4] - int(cos(r[5])*16)
	endif
	if fire <> 0 & freeBullets(0) > 0 then call Fire.Bullet()
endproc
'
'		Fire bullet
'
proc Fire.Bullet()
	if time() > tmr.last.fire
		local r,player
		tmr.last.fire = time()+20
		n = freeBullets(freeBullets(0))
		freeBullets(0) = freeBullets(0)-1
		r = objectList[n]
		player = objectList[0]
		call Set.Object.Velocity(r,2.5,player[5])
		r[1] = player[1]:r[3] = player[3]:r[0] = $BA:r[6] = n
		killBulletTime(n) = time()+100
	endif
endproc
'
'		Check kill bullets
'
proc Check.Kill.Bullets()
	local i
	for i = 1 to 4 
		if killBulletTime(i) > 0  & time() > killBulletTime(i) 
			call Kill.Bullet(i)
		endif
	next 
endproc
'
'		Kill Bullet
'
proc Kill.Bullet(n)
	local r:r = objectList[n]
	r[0] = 0:sprite r[6] hide
	killBulletTime(n) = 0
	freeBullets(0) = freeBullets(0)+1
	freeBullets(freeBullets(0)) = n
endproc
'
'		Draw Lives
'
proc DrawLives()
	local i
	if lives > 1
		for i = 1 to lives-1:image $BB frame to i*8+10,8:next
	endif
endproc
'
'		Draw Score
'
proc DrawScore()
	local i,a$
	rect from 9,0 solid ink 0 to 50,8
	a$ = right$("000000"+str$(score),6)
	for i = 1 to len(a$)
		image asc(mid$(a$,i,1))+$80 frame to i*6,-4
	next
endproc
stop