'
'	*** Atic Atac ***
'
call initialise.game()
call draw.room()
tMove = 0:tMissile = 0:tCheck = 0
repeat
	if event(tMove,1) then call move.player()
	if event(tMissile,2) then if tMissileEnd >= 0 then call move.missile()
	if event(tCheck,25) then call check.open.close()
until false
end
'
'		initialise game
'
proc initialise.game()
	gload "aticatac.gfx"
	game.data = alloc(2048):load "aticatac.dat",game.data
	' initialise level and dimensions, put at start
	size = 12:max.depth = 5
	level = 0:x.start = 4:y.start = 4
	' door records, created when room entered.
	dim doors(3): ' Values 0-7 see map.py
	dim door.open(3): ' true if door is open, false if shut/wall.
	dim door.timer(3): ' timer for door opening/closing
	' initialise player
	lives = 3:score = 0:x.room = x.start:y.room = y.start
	x.player = 128:y.player = 120:xi.player = 0:yi.player = 0:x.flip = 0
	dim key.count(3): ' key counts 1-3
	for i = 1 to 3:key.count(i) = 0:next
endproc
'
'		Move Player.
'
proc move.player()
	local fire 
	fire = joypad(xi.player,yi.player)
	if xi.player|yi.player
		x.player = x.player + (xi.player << 1)
		if x.player then x.flip = (1-xi.player)>>1
		y.player = y.player + (yi.player << 1)
		sprite 0 image $D6 + (((x.player+y.player) >> 3) & 1) flip x.flip to x.player,y.player
		if abs(x.player-128) > 56 | abs(y.player-120) > 56 then call hit.wall()
		if floor.centre <> 0
			if abs(x.player-128) < 16 & abs(y.player-120) < 16 
				if floor.centre = $D5
					level = level - 1:call animate.up():call draw.room()
				else
					level = level + 1:call animate.down():call draw.room()
				endif
			endif
		endif
	endif
	if fire <> 0 
		if tMissileEnd < 0
			tMissileEnd = time()+150
			x.missile = x.player:y.missile = y.player
			xi.missile = xi.player*3:yi.missile = yi.player*3
			while (xi.missile|yi.missile) = 0
				xi.missile = (rand(3)-1)*3:yi.missile = (rand(3)-1)*3
			wend			
		endif
	endif
endproc
'
'		Move missile
'
proc move.missile()
	sprite 1 image $D8 to x.missile,y.missile
	x.missile = x.missile + xi.missile
	y.missile = y.missile + yi.missile
	if abs(x.missile-128) > 58 then xi.missile = -xi.missile
	if abs(y.missile-120) > 58 then yi.missile = -yi.missile
	if time() > tMissileEnd then sprite 1 hide:tMissileEnd = -1
endproc
'
'
'		Hit Wall
'
proc hit.wall()
	local d,moved:moved = false
	x.player = max(128-56,min(x.player,128+56))
	y.player = max(120-56,min(y.player,120+56))
	if abs(x.player-128) >= 56 & abs(y.player-120) < 10
		d = -1 if x.player > 128 then d = 1
		call check.unlock(d & 3)
		if door.open(d & 3) then x.room = (x.room + d):x.player = 256-x.player:moved = true
	endif
	if abs(y.player-120) >= 56 & abs(x.player-128) < 10
		d = 0 if y.player > 128 then d = 2
		call check.unlock(d & 3)
		if door.open(d & 3) then y.room = (y.room + d - 1) y.player = 240-y.player:moved = true
		endif
	if moved then call draw.room()
endproc
'
'		Check unlocked in direction d
'
proc check.unlock(d)
	if (doors(d) & 3) <> 0 & door.open(d) = 0
		if key.count(doors(d) & 3) > 0 
			key.count(doors(d) & 3) = key.count(doors(d) & 3)-1
			doors(d) = 4:door.open(d) = true:door.timer(d) = -1
			local a:a = game.data + x.room + y.room*size + level*size*size
			if d = 0 then poke a-size,(peek(a-size) & $C7) + $20
			if d = 1 then poke a,(peek(a) & $F8) + $04
			if d = 2 then poke a,(peek(a) & $C7) + $20
			if d = 3 then poke a-1,(peek(a-1) & $F8) + $04
		endif
	endif
endproc
'
'		Draw room
'
proc draw.room()
	local i,n,g
	cls:sprite clear
	call random.seed.room(x.room,y.room,level)
	if level = 0:call draw.room.outline(2):else:call draw.room.outline(1):endif
	ink 3:print chr$(20);level;" ";x.room;",";y.room;" ";n
	' First is right, second is down
	call read.map(n,level,x.room,y.room-1):call make.door(0,n >> 3)
	call read.map(n,level,x.room-1,y.room):call make.door(3,n)
	call read.map(n,level,x.room,y.room):call make.door(1,n):call make.door(2,n >> 3)
	floor.centre = 0: ' $D4 pit, $D5 ladder
	if n & $40
		floor.centre = $D4+rand(2)
		if level = 0 then floor.centre = $D4
		if level = max.depth-1 then floor.centre = $D5
	endif
	for i = 0 to 3
		call random.next()
		if (seed & $E0) = 0 & level = 0
			n = 32:if (seed & 2) then n = -n
			call draw.wall.element(i,n,$D0 + (seed & 1),2)	
		endif
	next
	if floor.centre <> 0 then image floor.centre to 128-16,120-16
	text "1 Up" dim 1 ink 3 to 277,4
	call draw.score():call draw.keys():call draw.energy()
	if lives > 0
		for n = 1 to lives
			image $D6 to (n-1)*16+258,54
		next
	endif
	sprite 0 image $D6 to x.player,y.player
	tMissileEnd = -1
endproc
'
'		read map
'
proc read.map(ref n,level,x,y)
	n = peek(game.data + x + y*size + level*size*size)	
endproc
'
'		check open/close in current room
'
proc check.open.close()
	local i,n
	for i = 0 to 3
		if door.timer(i) > 0 & time() > door.timer(i)
			n = 150:if door.open(i) then n = 400
			door.timer(i) = time()+n
			door.open(i) = door.open(i) = 0
			call draw.door(i)
		endif
	next
endproc
'
'		create doors in given direction using preset random seed.
'
proc make.door(d,n)
	n = n & 7
	doors(d) = n:door.open(d) = false:door.timer(d) = -1
	if n = 4 then door.open(d) = true
	if n > 4 then door.timer(d) = rand(400)+100
	if n > 0 then call draw.door(d)			
	'door.open(d) = true
	print d,doors(d),door.open(d),door.timer(d)
endproc
'
'		draw door
'
proc draw.door(d)
	local img:img = $C8:if level > 0 then img = $C0
	if d <> 4 
		if door.open(d) = false then img = img + (doors(d) & 3)
	endif
	call draw.wall.element(d,0,img,4)
endproc
'
'		draw offset element
'
proc draw.wall.element(d,offset,g,rot)
	local x,y,f
	x = 128 + offset - 16:y = 120-64::f = 0
	if d = 2 then f = 2:y = 272-y
	if d = 1 | d = 3 
		g = g + rot:x = 128-64-32:f = d >> 1:y = 120+16+offset
		if d = 1 then x = 128+64
	endif
	rect x,y-31 solid ink 0 by 30,30
	image g,f to x,y-32 
endproc
'
'		draw the energy bar
'
proc draw.energy()
	local w,y:y = 38:w = 63
	rect 256,y ink 1 solid to 256+w,y+12
	rect 256,y ink 7 frame to 319,y+12
endproc
'
'		draw the score
'
proc draw.score()
	text right$("00000"+str$(score),5) ink 6 dim 2 solid to 260,16
endproc
'
'		draw the keys
'
proc draw.keys()
	local i,x,y:y = 80
	rect 256,y+6 ink 0 to 319,y+40
	for i = 1 to 3:
		x = 256 + (i-1) * 21 - 5
		image $D9+i dim 1 frame to x,y
		text right$("00"+str$(key.count(i)),2) to x+11,y + 24
	next
endproc
'
'		animate up ladder
'
proc animate.up()
	local y
	for y = -16 to 16
		sprite 0 image $D6+((y >> 2) & 1) to 128,y+120
		wait 10
	next
	y.player = 120+32:x.player = 128
endproc
'
'		animate down pit
'
proc animate.down()
	local i,j,w:cls
	for i = 1 to 5
		for j = 0 to 10
			w = j*10
			rect frame 128-w,120-w ink 7 to 128+w,120+w
			wait 2
			rect frame 128-w,120-w ink 0 to 128+w,120+w
		next
	next
endproc

'
'		draw outline room type 1 (cave) type 2 (room)
'
proc draw.room.outline(type)
	cx = -1
	plot ink 7 to 128,129
	if type = 1
		call line.draw(0,50)
		call line.draw(12,56):call line.draw(24,49):call line.draw(40,57)
		call line.draw(52,52):call line.draw(47,35):call line.draw(55,15)
		call line.draw(50,0)
	else
		call line.draw(0,50)
		call line.draw(45,58)
		call line.draw(58,45)
		call line.draw(50,0)
	endif
	rect 64,56 solid ink 0 to 192,184
	rect 64,56 frame ink 7 to 192,184
endproc
'
'		line.draw drawer, support routine for room draw, draws in four quadrants
'
proc line.draw(x,y)
	local x1,y1:x1 = x*2+128:y1 = y*2+120
	if cx >= 0
		if y <> 0
			line 128,120 to x1,y1:line 128,120 to 256-x1,y1
			line  128,239-120 to x1,239-y1:line 128,239-120 to 256-x1,239-y1
		endif
		line cx,cy to x1,y1
		line 256-cx,cy to 256-x1,y1
		line cx,239-cy to x1,239-y1
		line 256-cx,239-cy to 256-x1,239-y1
	endif
	cx = x1:cy = y1
endproc
'
'		random functions
'
proc random.seed(n)
	seed = n 
endproc
'
proc random.seed.room(x,y,l)
	x = x & room.mask:y = y & room.mask
	seed = x * 37 + level * 1234567 + y * 617
endproc
'
proc random.next()
	local i,bit0
	if seed = 0 then seed = $12345678
	for i = 1 to 4
		bit0 = seed & 1
		seed = seed >> 1
		if bit0 then seed = seed ^ $B400
	next
endproc

' Create monsters and move them. Type changes : invulnerable, speed, intelligence
' Scoring system , shooting
' Energy, dropping energy/key objects, collection
' SFX
