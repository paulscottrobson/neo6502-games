'
'		*** Frogger ***
'
cls:sprite clear:gload "frogger.gfx"
call Initialise()
for i = 0 to 9:call CreateMap(maps(i)+3,i):next
for i = 0 to 9:xPos(i) = rand(64):timer(i) = 0:next
rect solid ink 5 from 0,223 to 319,223-7*16
rect solid ink 4 from 0,0 to 319,32
rect solid ink 2 from 0,16 to 319,64
text "1-UP" frame ink 7 to 148,0
text "000000" frame ink 1 to 142,8
for i = 0 to 4:x = (i+1)*320\6:rect solid ink 4 from x-10,18 to x+10,64:next
for i = 0 to 2:image $80 to i * 20 + 4,224:next
sprite 0 image $80 to 160,216
repeat
	t = t + 1:if t = 10 then t = 0
	if event(timer(t),speed(t)) 
		tilemap maps(t),xPos(t),0
		tiledraw 0,yPos(t) to 320,yPos(t)+16
		xPos(t) = (xPos(t) + direction(t)) & mapMask
	endif
until false
end
'
'		Initialise game
'
proc Initialise()
	local i
	dim maps(9),xPos(9),speed(9),timer(9),yPos(9),direction(9):mapSize = 64
	for i = 0 to 9
		maps(i) = alloc(mapSize * 2 + 3)
		poke maps(i),1:poke maps(i)+1,mapSize * 2:poke maps(i)+2,1
		yPos(i) = (9-i)*16+48:if i >= 5 then yPos(i) = yPos(i) - 16
		read speed(i),direction(i)
	next

	mapMask = 16*mapSize-1
	data 6,1, 	5,-1, 	5,1, 	4,-3, 	9,1
	data 7,1, 	6,-1, 	4,-1, 	5,1,	6,-2
endproc
'
'		Create a single map.
'
proc CreateMap(map,row)
	local pos,i,c
	c = $F8:if row >= 5 then c = $F4
	for i = 0 to mapSize*2-1:poke map+i,c:next
	pos = 0
	while pos < mapSize-3
		call CreateElement(row)
		pos = pos + 2 + rand(3):if row >= 5 then pos = pos + 1
		if row = 3 then pos = pos + 3 + rand(3)
	wend
endproc
'
'		Create an element
'
proc CreateElement(row)
	local i
	if row < 5
		call WriteElement(pos,row):pos = pos + 1
		if row = 4 then call WriteElement(pos,5):pos = pos + 1
	else
		if row = 5 | row = 8
			for i = 1 to rand(2)+2:call WriteElement(pos,9):pos = pos + 1:next
		else
			call WriteElement(pos,6):pos = pos + 1
			for i = 1 to rand(3)+1:call WriteElement(pos,7):pos = pos + 1:next
			call WriteElement(pos-1,8)
		endif
	endif
endproc
'
proc WriteElement(p,c)
	if p < mapSize then poke map+p,c:poke map+p+mapSize,c
endproc
